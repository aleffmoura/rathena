//===== rAthena Script =======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= These functions handle Salvar, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed Salvar from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Salvar your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/Salvar arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to Usar Armazém
//=   arg(4): Cost to Alugar Carrinho
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome) {
	default:// Default message (obsolete)
		mes "[Funcionária Kafra]";
		mes "Bem vindo à Corporação Kafra. Os serviços da Corporação Kafra sempre estarão ao seu lado. Como posso lhe ajudar ?";
		break;
		break;
	case 1:// Niflheim specific message
		mes "[Funcionária Kafra]";
		mes "^666666B-Be-Beeeemmm V-vindooo";
		mes "a c-cooorpoor-r-raaçãaaoo";
		mes "K-k-Kafraaaa...^000000";
		break;
	case 2:// Guild Castle Kafra message
		@GID = getcharid(2);
		mes "[Funcionária Kafra]";
		mes "Bem vindo membro da guilda ^ff0000" + getguildname(@GID) + "^000000 .";
		mes "A Corporação Kafra, está aqui para atender você.";
		break;
	case 3:// Amatsu specific message (obsolete)
		mes "[Funcionária Kafra]";
		mes "Então, você vem de uma terra distante para estudar nossa cultura, ou você só está observando?";
		mes "Em ambos os casos, porque não permanecer por algum tempo?";
		mes "Com a eterna fragância";
		mes "do aroma de flores silvestres.";
		break;
	case 4:// Louyang and Ayothaya specific message (obsolete)
		mes "[Funcionária Kafra]";
		mes "Com tantas Kafras";
		mes "espalhadas, você nunca";
		mes "estará longe de casa.";
		break;
	case 5:// NPC has it's own welcome message. (Display nothing)
		break;
	}
	next;
	if (.@welcome == 2)// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
		setarray .@K_Menu0$[0], "Usar Armazém", "Usar Armazém do Clã", "Alugar Carrinho", "Usar Serviço de Teleporte", "Cancelar";
	else {
		switch(.@menu_num) {
			// Salvar and Storage only
			case 1:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
			// Storage only
			case 2:	setarray .@K_Menu0$[0],"Usar Armazém","Cancelar"; break;
			// No Teleport (Common)
			case 3: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Check Other Information","Cancelar"; break;
			// Case 4 is Einbroch no tele message.
			// No Salvar, or teleport. (Common)
			case 5:	setarray .@K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Check Other Information","Cancelar"; break;
			// Storage and Check Other Information only.
			case 6:	setarray .@K_Menu0$[0],"Usar Armazém","Check Other Information","Cancelar"; break;
			// Salvar, Storage, and Pushcart only (Kafra Warehouse)
			case 7:	setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar"; break;
			// Salvar, Storage, Other Check information. (Turbo track)
			case 8: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Check Other Information","Cancelar"; break;
			// No Salvar (Rune Knight)
			case 9: setarray .@K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Usar Serviço de Teleporte","Check Other Information","Cancelar"; break;
			// Storage, Salvar, and Pushcart (Dewata, reorder of case 7)
			case 10: setarray .@K_Menu0$[0],"Usar Armazém","Salvar","Alugar Carrinho","Cancelar"; break;
			// Default message (obsolete)
			default: setarray .@K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teleporte","Alugar Carrinho","Check Other Information","Cancelar"; break;
		}
	}
	.@menu$ = implode(.@K_Menu0$,":");
	while (true) {
		.@j = select(.@menu$) - 1;
		if (.@K_Menu0$[.@j] == "Salvar")
			return;
		else if (.@K_Menu0$[.@j] == "Usar Armazém") {
			if (.@welcome == 2)// Do not charge for Guild Storage
				callfunc "F_KafStor",2,0,0,"[Funcionária Kafra]";
			else
				callfunc "F_KafStor",0,getarg(3),.@welcome,"[Funcionária Kafra]";
			next;
		}
		else if (.@K_Menu0$[.@j] == "Usar Serviço de Teleporte") {
			if (.@menu_num != 4)// Display Einbroch "No Teleport Service" notice.
				callfunc "F_KafTele",.@welcome,"[Funcionária Kafra]";
			else {
				mes "[Funcionária Kafra]";
				mes "Por causa do ^FF0000Acordo de limite";
				mes "de transporte^000000, a";
				mes "Corporação Kafra não pode";
				mes "fornecer serviços de teletransporte";
				mes "aqui na república de Schwartzwald.";
				next;
				mes "[Funcionária Kafra]";
				mes "Nós solicitamos a você utilizar";
				mes "o serviço de Aeroplano no lugar";
				mes "do teletransporte. Muito obrigado por sua";
				mes "compreensão e cooperação.";
			}
			next;
		}
		else if (.@K_Menu0$[.@j] == "Alugar Carrinho") {
			if (callfunc("F_KafCart",.@welcome,getarg(4)) == 1)
				next;
		}
		else if (.@K_Menu0$[.@j] == "Check Other Information") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (.@K_Menu0$[.@j] == "Cancelar") {
			callfunc "F_KafEnd",.@welcome,0;
			end;
		}
		else if (.@K_Menu0$[.@j] == "Usar Armazém do Clã") {
			callfunc "F_KafStor",1,0,"[Funcionária Kafra]";
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);
	.@npc_name$ = getarg(3);
	// Unable to access Guild Storage (Busy)
	if (.@type == 1) {
		if (guildopenstorage()) {
			mes .@npc_name$;
			mes "Sinto muito, mas outro membro do Clã está usando o armazém neste instante.";
			mes "Por favor, aguarde até que esta pessoa termine.";
			close3;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (!callfunc("F_CanOpenStorage")) {
		mes .@npc_name$;
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-D-ddeessssculllpppe,";
			mes "v-vo-voooccêe éee umm";
			mes "Appreenddizz... P-Prreeecisa teerrr";
			mes "nívveelll d-de hh-habibilidaddeee level 6...^000000";
			return;
		}
		mes "Lamento, mas você";
		mes "precisa ser um Aprendiz com ";
		mes "nível mínimo de habilidade";
		mes "level 6 para usar o armazém.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (.@type != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(7059))
			delitem 7059,1;
		else {
			if (Zeny < .@fee) {
				mes .@npc_name$;
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "P-Preeciso de Z-Zeny...!";
					mes "P-Preciiiso de 150... Zenys...";
					mes "Ergh!!! T-tomando e-energia!";
					return;
				}
				// Standard Message
				mes "Lamento, mas você não possui";
				mes "Zenys suficientes para usar";
				mes "o serviço de armazém.";
				mes "Para acessar o armazém o custo é: "+.@fee+" Zenys.";
				return;
			}
			Zeny -= .@fee;
			RESRVPTS += (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes .@npc_name$;
			for ( .@i = 0; .@i < 5; .@i++ )
				mes "^666666Thank you.. for... using...^000000";
		}
		// Normal message
		else {
			mes .@npc_name$;
			mes "Aqui, deixe-me abrir";
			mes "seu Armazém para você.";
			mes "Obrigado por usar nossos serviços.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	.@npc_name$ = getarg(3);
	mes .@npc_name$;
	mes "Por favor selecione";
	mes "seu destino.";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Cancelar")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes .@npc_name$;
			mes "Sinto muito, mas você não possui";
			mes "Zenys para teleportar.";
			mes "O serviço de teleporte ";
			mes "para "+@wrpD$[.@j]+" is "+@wrpP[.@j]+" Zenys.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") {
		if (checkre(RENEWAL) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 91, 105;
	}
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	.@npc_name$ = getarg(2);
	// Ensure that the class wanting to Alugar Carrinho is a merchant
	if (BaseClass != Job_Merchant) {
		mes .@npc_name$;
		mes "Eu lamento, mas o serviço de aluguel de carrinho está disponível somente para";
		if (checkre(RENEWAL) == true)
			mes " classes de mecânicos.";
		else {
			mes " Mercadores, Ferreiros, Mestre Ferreiros, Alquimistas e Criadores.";
		}
		
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if (checkcart() == true) {
		mes .@npc_name$;
		mes "Você já possui";
		mes "o Carrinho equipado.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if (getskilllv("MC_PUSHCART") == 0) {
		mes .@npc_name$;
		mes "Você não pode alugar um carrignho enquanto não aprender a habilidade \"Empurrar Carrinho\".";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(7061) > 0 && getarg(0) != 2)
		delitem 7061,1;
	else {
		.@rental_fee = getarg(1);
		mes .@npc_name$;
		mes "O aluguel do carrinho de mão";
		mes "custará "+.@rental_fee+" Zenys.";
		mes "Você deseja alugar um carrinho de mão?";;
		next;
		if (select("Alugar Carrinho.:Cancelar") == 2)
			return 0;
		if (Zeny < .@rental_fee) {
			mes .@npc_name$;
			mes "Lamento, mas você não possui Zenys suficientes para alugar um carrinho.";
			mes "o aluguel custa "+.@rental_fee+" Zenys.";
			return 1;
		}
		Zeny -= .@rental_fee;
		RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
// Uncomment next line to block Kafra Storage Protection
//	.@block = 1;
	if (getarg(0) == 2)// Port Malaya, Rune Knight
		setarray .@menu$[0], "Check Special Reserve Points.", "", "", "Cancelar";
	else if (.@block)
		setarray .@menu$[0], "Check Special Reserve Points.", "", "Kafra Employee Locations", "Cancelar";
	else
		setarray .@menu$[0], "Check Special Reserve Points.", "Storage Password Service", "Kafra Employee Locations", "Cancelar";
	.@menu_list$ = implode(.@menu$,":");
	while (true) {
		.@j = select(.@menu_list$) - 1;
		switch(.@j) {
		case 0:// Check Special Reserve Points.
			mes "[Funcionária Kafra]";
			mes "Vamos ver...";
			mes strcharinfo(0) + "...";
			mes "Ah, você tem um total de ";
			mes ""+RESRVPTS+ " reservas de pontos especiais.";
			next;
			mes "[Funcionária Kafra]";
			mes "Você pode trocar suas";
			mes "Reservas de pontos especiais por";
			mes "recompensas no escritório da Corporação Kafra em Aldebaran. Por favor,";
			mes "utilize nossos serviços convenientes receber os benefícios do nosso programa de recompensas.";
			if (getarg(0) == 1)
				return;
			next;
			break;
		case 1:// Storage Password Service
			callfunc("F_SetKafCode","[Funcionária Kafra]","Kafra Services");
		case 2:// Kafra Employee Locations
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			break;
		default:
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if Salvar message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	.@type = getarg(0);
	mes "[Funcionária Kafra]";
	if (getarg(1) == true) {// Salvar
		mes "Seu ponto de renascimento";
		mes "foi salvo aqui.";
		mes ""+getarg(2)+".";
		mes "Muito obrigado por usar";
		mes "os serviços da Corporação Kafra.";
	} else if (.@type == 0 || .@type == 5) {// Generic End
		mes "Nós da Corporação Kafra,";
		mes "sempre estamos nos esforçando para proporcionoar os melhores serviços. Esperamos satisfazer as suas necessidades de aventureiro nos seus padrões de excelência.";
	} else if (.@type == 1) {// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra n-nunca";
		mes "mooooorrrrre. Semprre...";
		mes "Es-ta  ao seeeuuu laaadooo~^000000";
	} else if (.@type == 2) {// RockRidge Salvar
		mes "Salvo.";
		mes "Obrigado pelo seu patrocínio.";
	}
	close3;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Entre com a senha do armazém:";
	input .@code_;
	if (.@code_ != #kafra_code) {
		dispbottom "Senha incorreta.";
		close3;
	}
	@kafcode_try = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if (#kafra_code == 0) {
		mes "" + .@comp_name$ + " orgulhosamente apresenta um novo serviço:";
		mes "Proteção de armazenamento adicional com uma senha.";
		next;
		if (select( "Set new password -> 5000z", "Cancelar" ) == 1)
			callsub( S_SET, .@npc_name$, .@comp_name$ );
		close3;
	}
	mes "Seu armazém está protegido com uma senha. O que você fará agora?";
	next;
	switch( select( "Change old password -> 5000z", "Remove storage password -> 1000z", "Cancelar" ) ) {
	case 1:
		mes .@npc_name$;
		mes "Primeiro, digite sua ^0000FFsenha antiga^000000.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code || .@code != #kafra_code) {
			mes "Wrong password. You can't set a new password.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		callsub( S_SET, .@npc_name$, .@comp_name$ );
		break;
	case 2:
		mes .@npc_name$;
		mes "Por favor, entre com a senha antes de sua retirada.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code) {
			mes "A senha não foi removida.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		mes .@npc_name$;
		if (Zeny < 1000) {
			mes "Você não possui Zenys.";
			emotion ET_MONEY;
			break;
		}
		Zeny -= 1000;
		if (.@code == #kafra_code) {
			#kafra_code = 0;
			mes "Você teve êxito ao colocar senha em seu armazém.";
			mes "Muito obrigado por usar os "+.@comp_name$+".";
			emotion ET_THANKS;
		} else {
			mes "Senha errada! Não devolverei seus 1000 Zenys.";
			mes "Por favor, na próxima entre com a senha correta.";
			emotion ET_SORRY;
		}
		break;
	case 3:
		break;
	}
	close3;

S_SET:
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	mes "Agora entre com sua ^FF0000nova senha^000000 para proteger seu armazém de roubos.";
	.@code = callfunc("F_EntKafCode");
	if (!.@code) {
		mes "A senha não foi trocada.";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if (Zeny < 5000) {
		mes "Você não possui Zeny suficiente.";
		emotion ET_MONEY;
		return;
	}
	Zeny -= 5000;
	#kafra_code = .@code;
	mes "Seu armazém está protegido com uma senha secreta.";
	mes "Obrigado por usar"+.@comp_name$+".";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Digite um número entre 1000~10000000:";
	@kafcode_try += 1;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Hack: Tentou colocar senha de armazenamento.";
		return 0;
	}
	if (input(.@code_) == 1) {
		mes "Você não pode usar uma senha tão grande.";
		return 0;
	}
	if (.@code_ < 1000) {
		mes "Você não pode usar uma senha tão curta.";
		return 0;
	}
	return .@code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " z";
	@wrpC$[ .@warp_size ] = "Cancelar";
	return;
}
